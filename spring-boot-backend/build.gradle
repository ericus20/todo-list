import com.github.spotbugs.SpotBugsTask

plugins {
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id 'com.github.spotbugs' version '1.6.9'
    id 'checkstyle'
    id 'eclipse'
    id 'java'
    id 'idea'
    id 'pmd'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.developersboard'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    implementation 'com.google.code.gson:gson:2.8.5'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation 'org.mockito:mockito-junit-jupiter:2.23.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '5.5.1'
}

// Checkstyle task to disable xml reports and load config file
tasks.withType(Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle.xml")
    include '**/*.java'
    exclude '**/gen/**'
    reports {
        xml.enabled = false
        html.enabled = true
    }

    classpath = files()
}

spotbugs {
    toolVersion = '3.1.12'
    sourceSets = [sourceSets.main]
    ignoreFailures = false
    effort = "max"
    showProgress = true
    reportLevel = "high"
    visitors = ["FindSqlInjection", "SwitchFallthrough"]
    omitVisitors = ["FindNonShortCircuit"]
    includeFilter = file("$rootProject.projectDir/config/spotbugs.xml")
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = false
    toolVersion = '6.17.0'
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/pmdReports")
    ruleSetFiles = files("$rootProject.projectDir/config/pmd-ruleset.xml")
    ruleSets = []
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
